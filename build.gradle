group 'lostfilm-kotlin'
version '1.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.2-2'
    ext.vertx_version = '3.4.1'
    ext.argparser_version = '2.0.1'
    ext.jackson_version = '2.8.7'
    ext.kovenant_version = '3.3.0'
    ext.shadow_version = '2.0.0'
    ext.junit_version = '4.11'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"

    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = "lostfilm.MainKt"
repositories {
    jcenter()

    mavenCentral()
}

sourceSets {
    main.kotlin.srcDirs += 'src'
    test {
        kotlin {
            srcDir "test"
        }
    }
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jackson_version"

    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile group: 'me.tongfei', name: 'progressbar', version: '0.5.5'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'

    compile 'ninja.sakib:PultusORM:v1.4'

    compile 'com.github.kittinunf.fuel:fuel:1.7.0'

    testCompile "junit:junit:$junit_version"
}

defaultTasks 'run'

task wrapper(type: Wrapper) {
    gradleVersion = "3.3"
}

shadowJar {
    baseName = project.name
    classifier = null
    version = null
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task script(dependsOn: 'shadowJar') {
    ext.destFile = "${project.buildDir}/${project.name}"
    ext.stub = 'stub.sh'
    ext.jarFile = "${project.buildDir}/libs/${project.name}.jar"
    inputs.file jarFile
    outputs.file destFile
    doLast {
        ant.concat(destfile: destFile, binary: true) {
            fileset(file: stub)
            fileset(file: jarFile)
        }
        ant.chmod(file: destFile, perm: "a+x")
    }
}


